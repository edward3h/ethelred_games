plugins {
    id 'application'
    id 'groovy' // for tests
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation group: 'io.javalin', name: 'javalin', version: '4.0.0.ALPHA0'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'com.github.callicoder:java-snowflake:a166af56e2'
    implementation libs.log4j.api
    implementation libs.bundles.jackson
    runtimeOnly libs.bundles.log4j.runtime

    implementation 'com.google.dagger:dagger:2.34'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.34'
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.guava:guava:30.1.1-jre'

    testImplementation 'org.codehaus.groovy:groovy-all:3.0.8'
    testImplementation ('org.spockframework:spock-core:2.0-M5-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }

    // ref https://evojam.com/technology-blog/2020/5/18/the-secret-of-painless-websocket-tests-with-spock-framework
    testImplementation 'com.neovisionaries:nv-websocket-client:2.14'

    testImplementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("--enable-preview")
}

tasks.withType(JavaExec).configureEach {
    jvmArgs("--enable-preview")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jvmArgs("--enable-preview")
}

application {
    mainClass = 'org.ethelred.games.server.Main'
}