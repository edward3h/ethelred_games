plugins {
    // Apply the java Plugin to add support for Java.
    id 'java'
    id 'groovy' // for tests
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation libs.log4j.api
    runtimeOnly libs.bundles.log4j.runtime

    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    testImplementation "org.apache.groovy:groovy:5.0.0-alpha-10"

    testImplementation ('org.spockframework:spock-core:2.4-M1-groovy-4.0') {
        exclude group: 'org.codehaus.groovy'
    }

    // used for mocking
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.14.11'
    testRuntimeOnly 'org.objenesis:objenesis:3.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            useJUnitJupiter()
        }
    }
}

// TODO: Re-enable Groovy tests when stable Groovy 5 + Spock combination is available
// Java 25 requires Groovy 5.x, but Spock doesn't have stable Groovy 5 support yet
tasks.named('compileTestGroovy') {
    enabled = false
}
tasks.withType(Test).configureEach {
    // Allow running with no discovered tests (since Groovy tests are disabled)
    failOnNoDiscoveredTests = false
}
